;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
;*              MODIFICAÇÕES PARA USO COM 12F675                   *
;*                FEITAS PELO PROF. MARDSON                        *
;*                    FEVEREIRO DE 2014                            *
;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *

;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
;*                       NOME DO PROJETO                           *
;*                           CLIENTE                               *
;*         DESENVOLVIDO PELA MOSAICO ENGENHARIA E CONSULTORIA      *
;*   VERSÃO: 1.0                           DATA: 17/06/03          *
;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
;*                     DESCRIÇÃO DO ARQUIVO                        *
;*-----------------------------------------------------------------*
;*   MODELO PARA O PIC 12F675                                      *
;*                                                                 *
;*                                                                 *
;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *

;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
;*                     ARQUIVOS DE DEFINIÇÕES                      *
;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
#INCLUDE <P12F675.INC>	;ARQUIVO PADRÃO MICROCHIP PARA 12F675

	__CONFIG _BODEN_OFF & _CP_OFF & _PWRTE_ON & _WDT_OFF & _MCLRE_ON & _INTRC_OSC_NOCLKOUT

;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
;*                    PAGINAÇÃO DE MEMÓRIA                         *
;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
;DEFINIÇÃO DE COMANDOS DE USUÁRIO PARA ALTERAÇÃO DA PÁGINA DE MEMÓRIA
#DEFINE	BANK0	BCF STATUS,RP0	;SETA BANK 0 DE MEMÓRIA
#DEFINE	BANK1	BSF STATUS,RP0	;SETA BANK 1 DE MAMÓRIA

;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
;*                         VARIÁVEIS                               *
;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
; DEFINIÇÃO DOS NOMES E ENDEREÇOS DE TODAS AS VARIÁVEIS UTILIZADAS 
; PELO SISTEMA

	CBLOCK	0x20	;ENDEREÇO INICIAL DA MEMÓRIA DE
					;USUÁRIO
		W_TEMP		;REGISTRADORES TEMPORÁRIOS PARA USO
		STATUS_TEMP	;JUNTO ÀS INTERRUPÇÕES
		DADO		;ARMAZENA O DADO PARA A EEPROM

		;NOVAS VARIÁVEIS
		TAMANHO		;ARMAZENA O TAMANHO DO "ARRAY"
		X		;CONTADOR DO LAÇO DE REPETIÇÃO EXTERNO
		Y		;CONTADOR DO LAÇO DE REPETIÇÃO INTERNO
		VN		;ARMAZENA O DADO QUE ESTA NA EEPROM USANDO O VALOR ATUAL DO CONTADOR COMO INDICE
		VN1		;ARMAZENA O DADO QUE ESTA NA EEPROM USANDO O PROXIMO VALOR DO CONTADOR COMO INDICE
		AUX		;ARMAZENA O VALOR TEMPORARIAMENTE NO SWAP
		CONT		;ARMAZENA UM VALOR USADO COMO PARAMETRO
		
	ENDC			;FIM DO BLOCO DE MEMÓRIA
;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
;*                        FLAGS INTERNOS                           *
;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
; DEFINIÇÃO DE TODOS OS FLAGS UTILIZADOS PELO SISTEMA

;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
;*                         CONSTANTES                              *
;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
; DEFINIÇÃO DE TODAS AS CONSTANTES UTILIZADAS PELO SISTEMA
; INICIALIZAÇÃO DA EEPROM, DE ACORDO COM A DESCRIÇÃO NO ARQUIVO "Def_Rega_Formigas.inc"

;A PARTIR DO ENDEREÇO ZERO DA EEPROM, DADOS EM ORDEM ALEATÓRIA
	ORG 0x2100
	DE	0X89,0X1E,0X39,0X9F,0XC2,0X0C,0XAB,0X33,0X63,0XD3,0X95,0X7B,0X38,0XD6,0X1E,0X48
	DE	0XDB,0XD8,0X86,0XFD,0XA5,0XFC,0X0C,0XBE,0X68,0X9B,0XD9,0X10,0XD8,0XEC,0X90,0X91
	DE	0XAA,0XBB,0XCC,0XDD,0XEE,0XF1,0XC9,0X77

;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
;*                           ENTRADAS                              *
;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
; DEFINIÇÃO DE TODOS OS PINOS QUE SERÃO UTILIZADOS COMO ENTRADA
; RECOMENDAMOS TAMBÉM COMENTAR O SIGNIFICADO DE SEUS ESTADOS (0 E 1)

;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
;*                           SAÍDAS                                *
;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
; DEFINIÇÃO DE TODOS OS PINOS QUE SERÃO UTILIZADOS COMO SAÍDA
; RECOMENDAMOS TAMBÉM COMENTAR O SIGNIFICADO DE SEUS ESTADOS (0 E 1)

;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
;*                       VETOR DE RESET                            *
;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *

	ORG	0x00		;ENDEREÇO INICIAL DE PROCESSAMENTO
	GOTO	INICIO
	
;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
;*                    INÍCIO DA INTERRUPÇÃO                        *
;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
; ENDEREÇO DE DESVIO DAS INTERRUPÇÕES. A PRIMEIRA TAREFA É SALVAR OS
; VALORES DE "W" E "STATUS" PARA RECUPERAÇÃO FUTURA

	ORG	0x04		;ENDEREÇO INICIAL DA INTERRUPÇÃO
	MOVWF	W_TEMP		;COPIA W PARA W_TEMP
	SWAPF	STATUS,W
	MOVWF	STATUS_TEMP	;COPIA STATUS PARA STATUS_TEMP

;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
;*                    ROTINA DE INTERRUPÇÃO                        *
;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
; AQUI SERÁ ESCRITA AS ROTINAS DE RECONHECIMENTO E TRATAMENTO DAS
; INTERRUPÇÕES

;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
;*                 ROTINA DE SAÍDA DA INTERRUPÇÃO                  *
;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
; OS VALORES DE "W" E "STATUS" DEVEM SER RECUPERADOS ANTES DE 
; RETORNAR DA INTERRUPÇÃO

SAI_INT
	SWAPF	STATUS_TEMP,W
	MOVWF	STATUS		;MOVE STATUS_TEMP PARA STATUS
	SWAPF	W_TEMP,F
	SWAPF	W_TEMP,W	;MOVE W_TEMP PARA W
	RETFIE

;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
;*	            	 ROTINAS E SUBROTINAS                      *
;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
; CADA ROTINA OU SUBROTINA DEVE POSSUIR A DESCRIÇÃO DE FUNCIONAMENTO
; E UM NOME COERENTE ÀS SUAS FUNÇÕES.
LE_EEPROM
;LER DADO DA EEPROM, CUJO ENDEREÇO É INDICADO EM W
;DADO LIDO RETORNA EM W
	ANDLW	.127		;LIMITA ENDEREÇO MAX. 127
	BANK1			;ACESSO VIA BANK 1
	MOVWF	EEADR		;INDICA O END. DE LEITURA
	BSF	EECON1,RD	;INICIA O PROCESSO DE LEITURA
	MOVF	EEDATA,W	;COLOCA DADO LIDO EM W
	BANK0			;POSICIONA PARA BANK 0
	RETURN

GRAVA_EEPROM
	;ESCREVE DADO (DADO) NA EEPROM, CUJO ENDEREÇO É INDICADO EM W
	ANDLW	.127		;LIMITA ENDEREÇO MAX. 127
	BANK1			;ACESSO VIA BANK 1
	MOVWF	EEADR
	MOVF	DADO,W
	MOVWF	EEDATA
	BSF	EECON1,WREN	;HABILITA ESCRITA
	BCF	INTCON,GIE	;DESLIGA INTERRUPÇÕES
	MOVLW	B'01010101'	;DESBLOQUEIA ESCRITA
	MOVWF	EECON2		;
	MOVLW	B'10101010'	;DESBLOQUEIA ESCRITA
	MOVWF	EECON2		;
	BSF	EECON1,WR	;INICIA A ESCRITA
	
AGUARDA
	BTFSC	EECON1,WR	;TERMINOU?
	GOTO	AGUARDA
	BSF	INTCON,GIE	;HABILITA INTERRUPÇÕES
	BANK0			;POSICIONA PARA BANK 0
	RETURN

SUBROTINA1
	;CORPO DA ROTINA
	RETURN

;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
;*                     INICIO DO PROGRAMA                          *
;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
	
INICIO
	BANK1			;ALTERA PARA O BANCO 1
	MOVLW	B'00000000'	;CONFIGURA TODAS AS PORTAS DO GPIO (PINOS)
	MOVWF	TRISIO		;COMO SAÍDAS
	CLRF	ANSEL 		;DEFINE PORTAS COMO Digital I/O
	MOVLW	B'00100100'	;DEFINE GP5 COMO SAÍDA
	MOVWF	OPTION_REG	;DEFINE OPÇÕES DE OPERAÇÃO
	MOVLW	B'00000000'
	MOVWF	INTCON		;DEFINE OPÇÕES DE INTERRUPÇÕES
	BANK0			;RETORNA PARA O BANCO
	MOVLW	B'00000111'
	MOVWF	CMCON		;DEFINE O MODO DE OPERAÇÃO DO COMPARADOR ANALÓGICO

;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
;*                     INICIALIZAÇÃO DAS VARIÁVEIS                 *
;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *

;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
;*                     ROTINA PRINCIPAL                            *
;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
	
MAIN
	BSF	GPIO, GP5	    ;ATIVA O GP5 PARA INDICAR O INICIO DO ALGORITMO
	MOVLW	.40		    ;W = 40
	MOVWF	TAMANHO		    ;TAMANHO = W;
	
INICIA_X
	MOVLW	.0		    ;W = 0	    
	MOVWF	X		    ;X = W
	
LACO_EXTERNO			    ;for(X = 0; X < TAMANHO - 1; X++)
	;X < TAMANHO-1
	DECF	TAMANHO,0	    ;W = TAMANHO - 1
	SUBWF	X,W		    ;W = X - W
	BTFSC	STATUS,Z	    ;SE X - (TAMANHO-1) != 0, Z = 0
	GOTO	FIM		    ;SE Z = 1, SAI DO LAÇO EXTERNO
	GOTO	INICIA_Y	    ;SE Z = 0, ENTRA NO LAÇO INTERNO    
	FLUXO_1
	INCF	X,1		    ;X = X+1
	GOTO	LACO_EXTERNO	    ;VAI PARA A PROXIMA ITERAÇÃO
	
INICIA_Y			    ;for(Y = 0; Y < TAMANHO-X-1; Y++)
	;Y = 0
	MOVLW	.0		    ;W = 0		    
	MOVWF	Y		    ;W = Y
	;CONTA = TAMANHO-1-X
	DECF	TAMANHO,0	    ;W = TAMANHO - 1
	MOVWF	CONT		    ;CONT = W
	MOVFW	X		    ;W = X
	SUBWF	CONT,1		    ;CONT = CONT - W
	
LACO_INTERNO
	;Y < TAMANHO-X-1
	MOVFW	CONT		    ;W = CONT
	SUBWF	Y,W		    ;W = Y - W
	BTFSC	STATUS,Z	    ;SE Y - (TAMANHO-X-1) != 0, Z = 0
	GOTO	FLUXO_1		    ;SE Z = 1, SAI DO LAÇO INTERNO
	
	;SE EEPROM[Y] > EEPROM[Y+1], TROCA
	MOVFW	Y		    ;W = Y
	CALL	LE_EEPROM	    ;LE O VALOR DA EEPROM NA POSIÇÃO Y
	MOVWF	VN		    ;GUARDA O DADO QUE ESTA NA POSIÇÃO Y DA EEPROM EM VN
	
	INCF	Y,0		    ;W = Y+1
	CALL	LE_EEPROM	    ;LE O VALOR DA EEPROM NA POSIÇÃO Y+1
	MOVWF	VN1		    ;GUARDA O DADO QUE ESTA NA POSIÇÃO Y DA EEPROM EM VN1


	SUBWF	VN,W		    ;W = VN - W, OU SEJA, W = VN - VN1
	BTFSC	STATUS,C	    ;SE VN - VN1 > 0, C = 1
	GOTO	TROCA		    ;VAI PARA O "SWAP"
	
	;VOLTA PARA CONTINUAR O LAÇO INTERNO
	FLUXO_2			    
	
	;Y++
	INCF	Y,1		    ;Y = Y+1	
	GOTO	LACO_INTERNO	    ;VAI PARA A PROXIMA ITERAÇÃO	    

TROCA
	;GUARDA O VALOR DE VN
	MOVFW	VN		    ;W = VN
	MOVWF	AUX		    ;AUX = W
	;GRAVA VN1 NA POSICAO DE VN
	MOVFW	VN1		    ;W = VN1
	MOVWF	DADO		    ;DADO = W
	MOVFW	Y		    ;W = Y
	CALL	GRAVA_EEPROM
	CLRF	DADO
	;GRAVA VN NA POSICAO DE VN1
	MOVFW	AUX		    ;W = AUX
	MOVWF	DADO		    ;DADO = W
	INCF	Y, 0		    ;W = Y+1
	CALL	GRAVA_EEPROM
	CLRF	DADO
	GOTO	FLUXO_2		    ;VOLTA PARA O FLUXO PRINCIPAL DO LAÇO INTERNO
		
FIM
	BCF	GPIO, GP5	    ;DESATIVA O GP5 PARA INDICAR Q O ALGORITMO TERMINOU
	GOTO FIM		    ;LOOP

;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
;*                       FIM DO PROGRAMA                           *
;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *

	END
